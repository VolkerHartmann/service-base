apply plugin: 'signing'
apply plugin: 'net.researchgate.release'

//Sonatype settings
group = "edu.kit.datamanager"
archivesBaseName = "service-base"

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

release {
    versionPropertyFile = 'gradle.properties'
    versionProperties = ['version', 'mainversion']
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

//            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
//                authentication(userName: ossrhUsername, password: ossrhPassword)
//            }

            pom.project {
                name 'service-base'
                packaging 'jar'
                // optionally artifactId can be defined here 
                description 'Base service module containing entities, interfaces and helpers for KIT Data Manager 2.0'
                url 'http://datamanager.kit.edu'

                scm {
                    connection 'scm:git:github.com/kit-data-manager/service-base'
                    developerConnection 'scm:git:github.com/kit-data-manager/service-base'
                    url 'https://github.com/kit-data-manager/service-base'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'Jejkal'
                        name 'Thomas Jejkal'
                        email 'webmaster@datamanager.kit.edu'
                    }
                }
            }
        }
    }
}
